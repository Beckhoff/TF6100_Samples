<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="PRG_CONNECT" Id="{002e8e07-f0b2-479e-99d2-5f4b67fd2a40}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM PRG_CONNECT
VAR
	(* Declarations for UA_Connect *)
	fbUA_Connect			: UA_Connect;
	SessionConnectInfo		: ST_UASessionConnectInfo;
	nConnectionHdl			: DWORD;

	(* Declarations for UA_Disconnect *)
	fbUA_Disconnect		: UA_Disconnect;

	(* Other declarations *)
	iState		: INT;
	bTest		: BOOL:= TRUE;
	bDone		: BOOL;
	bBusy		: BOOL;
	bError		: BOOL;
	nErrorID	: DWORD;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
CASE iState OF
0:	(* idle *)
	IF bTest THEN
		bTest		:= FALSE;
		bError		:= FALSE;
		nErrorID	:= 0;

		SessionConnectInfo.tConnectTimeout 		:= T#1M;
		SessionConnectInfo.tSessionTimeout 		:= T#1M;
		SessionConnectInfo.sApplicationName		:= '';
		SessionConnectInfo.eSecurityMode 		:= eUASecurityMsgMode_Sign_Encrypt;
		SessionConnectInfo.eSecurityPolicyUri 	:= eUASecurityPolicy_Basic256Sha256;
		SessionConnectInfo.eTransportProfileUri := eUATransportProfileUri_UATcp;

		(*
		SessionConnectInfo.stUserIdentTokenType.eUserIdentTokenType := eUAUITT_Username;
		SessionConnectInfo.stUserIdentTokenType.sTokenParam1 := 'Administrator';
		SessionConnectInfo.stUserIdentTokenType.sTokenParam2 := 'SomeSecurePassword';
		*)
		
		iState	:= iState + 1;
	END_IF

1:	(* Open UA session *)
	fbUA_Connect(
		Execute				:= TRUE,
		ServerURL			:= sOpcUaServerUrl,
		SessionConnectInfo	:= SessionConnectInfo,
		Timeout				:= T#2M,
		ConnectionHdl		=> nConnectionHdl
	);

	IF NOT fbUA_Connect.Busy THEN
		fbUA_Connect(Execute := FALSE);

		IF NOT fbUA_Connect.Error THEN
			(* session open *)
			iState := iState + 1;
		ELSE
			bError			:= TRUE;
			nErrorID		:= fbUA_Connect.ErrorID;
			nConnectionHdl	:= 0;
			iState 			:= 0; (* idle *)
		END_IF
	END_IF

2:	(* close session *)
	fbUA_Disconnect(
		Execute			:= TRUE,
		ConnectionHdl	:= nConnectionHdl
	);

	IF NOT fbUA_Disconnect.Busy THEN
		fbUA_Disconnect(Execute := FALSE);
		bBusy := FALSE;
		IF NOT fbUA_Disconnect.Error THEN
			(* session closed *)
			iState 		:= 0;
			IF NOT bError THEN 
				bDone 	:= TRUE;
			END_IF
		ELSE
			bError			:= TRUE;
			nErrorID		:= fbUA_Disconnect.ErrorID;
			iState 			:= 0; (* idle *)
			nConnectionHdl	:= 0;
		END_IF
	END_IF
END_CASE
]]></ST>
    </Implementation>
    <LineIds Name="PRG_CONNECT">
      <LineId Id="3" Count="13" />
      <LineId Id="241" Count="0" />
      <LineId Id="236" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="238" Count="2" />
      <LineId Id="237" Count="0" />
      <LineId Id="23" Count="15" />
      <LineId Id="235" Count="0" />
      <LineId Id="40" Count="8" />
      <LineId Id="136" Count="22" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>